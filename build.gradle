plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.linguica.maven-settings' version "${maven_settings_version}"
}

group = 'de.maxhenkel.opus4j'
archivesBaseName = 'opus4j'
version = library_version

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

tasks.named('sourcesJar', Jar) {
    exclude 'natives/**'
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation platform("org.junit:junit-bom:${junit_version}")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.platform:junit-platform-launcher'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    exclude '**/*.gitkeep'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Opus4J'
                description = 'Opus Wrapper for Java'
                url = 'https://maxhenkel.de'
            }
        }
    }
    repositories {
        maven {
            name = 'henkelmax.public'
            url = uri('https://maven.maxhenkel.de/repository/public')
        }
    }
}

def platforms = [
        [
                name  : 'windows-x64',
                file  : 'zig-x86_64-windows-gnu.cmake',
                res   : 'windows-x64',
                libExt: 'dll'
        ],
        [
                name  : 'windows-arm64',
                file  : 'zig-aarch64-windows-gnu.cmake',
                res   : 'windows-aarch64',
                libExt: 'dll'
        ],
        [
                name  : 'linux-x64',
                file  : 'zig-x86_64-linux-gnu.cmake',
                res   : 'linux-x64',
                libExt: 'so'
        ],
        [
                name  : 'linux-arm64',
                file  : 'zig-aarch64-linux-gnu.cmake',
                res   : 'linux-aarch64',
                libExt: 'so'
        ],
        [
                name  : 'macos-x64',
                file  : 'zig-x86_64-macos.cmake',
                res   : 'macos-x64',
                libExt: 'dylib'
        ],
        [
                name  : 'macos-arm64',
                file  : 'zig-aarch64-macos.cmake',
                res   : 'macos-aarch64',
                libExt: 'dylib'
        ],
]

platforms.each { target ->
    def safeName = target.name.capitalize().replace('-', '')
    def configureTask = "configureNatives${safeName}"
    def buildTask = "buildNatives${safeName}"
    def copyTask = "copyNatives${safeName}"

    def cmakeListsPath = "../../CMakeLists.txt"
    def cmakePlatformPath = "../../cmake/${target.file}"
    def buildDir = layout.projectDirectory.dir("native/build/${target.res}")
    def resourceDir = layout.projectDirectory.dir("src/main/resources/natives/${target.res}")
    def cSourcesDir = layout.projectDirectory.dir('native/src')
    def outputName = "libopus4j.${target.libExt}"

    tasks.register(configureTask, Exec) {
        group = 'natives'
        description = "CMake configure for ${target.name}"
        workingDir = buildDir

        inputs.file(buildDir.file(cmakeListsPath))
        inputs.file(buildDir.file(cmakePlatformPath))
        inputs.dir(cSourcesDir)
        outputs.dir(buildDir)

        doFirst {
            def dir = buildDir.asFile
            if (dir.exists()) {
                logger.lifecycle("Cleaning ${dir}")
                project.delete(fileTree(dir: dir))
            }
            dir.mkdirs()
        }

        commandLine 'cmake',
                "-DCMAKE_TOOLCHAIN_FILE=${cmakePlatformPath}",
                '-DCMAKE_BUILD_TYPE=Release',
                '-G', 'Ninja',
                '../..'
    }

    tasks.register(buildTask, Exec) {
        group = 'natives'
        description = "Build natives for ${target.name}"
        workingDir = buildDir
        dependsOn configureTask

        commandLine 'cmake', '--build', '.', '--config', 'Release'
    }

    tasks.register(copyTask, Copy) {
        group = 'natives'
        description = "Copy ${target.name} natives into resources"
        dependsOn buildTask

        def libraryFile = buildDir.asFile.toPath().resolve(outputName).toFile()
        inputs.file(libraryFile)
        outputs.dir(resourceDir)

        from libraryFile
        into resourceDir
    }
}

tasks.register('buildAndCopyAllNatives') {
    group = 'natives'
    description = 'Build & copy all native libraries for every platform/arch'
    dependsOn platforms.collect { "copyNatives${it.name.capitalize().replace('-', '')}" }
}

tasks.named('processResources') {
    dependsOn 'buildAndCopyAllNatives'
}
